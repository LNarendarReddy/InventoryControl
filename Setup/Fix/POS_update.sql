USE [C:\Windows\NSRetailPOS\NSRETAILPOS.MDF]
GO
:ON ERROR EXIT
GO

IF EXISTS(SELECT 1 FROM SYS.tables WHERE [name] = 'PAYMENTGATEWAYINFO')
BEGIN
	EXEC ('DROP TABLE PAYMENTGATEWAYINFO')
END
GO

IF NOT EXISTS(SELECT 1 FROM SYS.tables WHERE [name] = 'PAYMENTGATEWAYINFO')
BEGIN

	EXEC 
	('
		CREATE TABLE PAYMENTGATEWAYINFO
			(
				PAYMENTGATEWAYINFOID INT PRIMARY KEY,
				PAYMENTGATEWAYINFONAME VARCHAR(100) NOT NULL,
				PAYMENTGATEWAYCONFIGDATA VARCHAR(MAX) NOT NULL,
				PAYMENTGATEWAYINFOTYPE VARCHAR(100) NOT NULL,
				CREATEDBY INT,
				CREATEDDATE DATETIME,
				UPDATEDBY INT,
				UPDATEDDATE DATETIME,
				DELETEDBY INT,
				DELETEDDATE DATETIME
			)
	')

END
GO


IF NOT EXISTS ( SELECT 1 FROM SYS.all_columns WHERE [name] = 'PAYMENTGATEWAYID' AND [object_id] =  OBJECT_ID('POS_BRANCHCOUNTER'))
BEGIN
	
	EXEC ('ALTER TABLE POS_BRANCHCOUNTER ADD PAYMENTGATEWAYID INT')

END
GO

IF NOT EXISTS ( SELECT 1 FROM SYS.all_columns WHERE [name] = 'PAYMENTGATEWAYADDITIONALCONFIG' AND [object_id] =  OBJECT_ID('POS_BRANCHCOUNTER'))
BEGIN
	
	EXEC ('ALTER TABLE POS_BRANCHCOUNTER ADD PAYMENTGATEWAYADDITIONALCONFIG VARCHAR(MAX)')

END
GO

/****** Object:  StoredProcedure [dbo].[POS_USP_R_USERCREDENTIALS]    Script Date: 11-05-2025 17:37:06 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER   PROCEDURE [dbo].[POS_USP_R_USERCREDENTIALS]          
@BRANCHID INT,          
@BRANCHCOUNTERID INT,          
@USERNAME NVARCHAR(100),          
@PASSWORDSTRING NVARCHAR(200),      
@HDDSNO NVARCHAR(50)      
AS          
BEGIN          
      
	IF NOT EXISTS (SELECT 1 FROM POS_BRANCHCOUNTER BC WHERE BC.COUNTERID = @BRANCHCOUNTERID AND BC.HDDSNO = @HDDSNO)      
	BEGIN      
		SELECT 'Machine identifier mismatch, application will exit'      
		RETURN      
	END      
          
	DECLARE @USERID INT          
	SELECT @USERID = USERID     
	FROM   
		POS_TBLUSER USR    
		INNER JOIN POS_TBLROLE R ON R.ROLEID = USR.ROLEID    
	WHERE (BRANCHID = @BRANCHID OR R.ROLENAME = 'Store Admin')    
		AND USERNAME = @USERNAME           
		AND PASSWORDSTRING = @PASSWORDSTRING    
		AND USR.DELETEDDATE IS NULL
  
	DECLARE @DBVersion NVARCHAR(50)  
	SELECT @DBVersion = CONFIGVALUE FROM TBLCONFIG WHERE CONFIGNAME = 'DBVersion'  
          
	IF(@USERID  > 0)          
	BEGIN          
          
		SELECT           
			USERID, USERNAME, PASSWORDSTRING, FULLNAME, CNUMBER, EMAIL, ISOTP, GENDER,          
			B.BRANCHID, BRANCHNAME, BRANCHCODE, ADDRESS, PHONENO, LANDLINE,    
			R.ROLENAME, B.MULTIEDITTHRESHOLD, @DBVersion AS DBVersion, B.FILTERMRPBYSTOCK , U.CATEGORYID 
			, B.ENABLEDRAFTBILLS
		FROM   
		POS_TBLUSER U     
		INNER JOIN POS_BRANCH B ON @BRANCHID = B.BRANCHID          
		INNER JOIN POS_TBLROLE R ON U.ROLEID = R.ROLEID    
		WHERE USERID = @USERID          
        
		SELECT 
			BC.COUNTERID, BC.COUNTERNAME, PG.PAYMENTGATEWAYINFOID, PG.PAYMENTGATEWAYINFONAME, PG.PAYMENTGATEWAYINFOTYPE
			, PG.PAYMENTGATEWAYCONFIGDATA, BC.PAYMENTGATEWAYADDITIONALCONFIG
		FROM 
			POS_BRANCHCOUNTER BC
			LEFT JOIN PAYMENTGATEWAYINFO PG ON PG.PAYMENTGATEWAYINFOID = BC.PAYMENTGATEWAYID
		WHERE COUNTERID = @BRANCHCOUNTERID        
          
	END          
	ELSE          
	BEGIN          
          
		SELECT 'Invalid Username or Password!'          
          
	END          
          
END 
GO


IF NOT EXISTS(SELECT 1 FROM SYS.tables WHERE [name] = 'POS_PGW_TRANSACTIONDATA')
BEGIN

	EXEC 
	('
		CREATE TABLE POS_PGW_TRANSACTIONDATA
			(
				TRANSACTIONDATAID INT IDENTITY(1, 1) PRIMARY KEY,
				BILLID INT NOT NULL,
				MOPID DECIMAL(18, 2) NOT NULL,
				AMOUNT DECIMAL(18, 2) NOT NULL,
				PAYMENTREQUEST VARCHAR(MAX),
				PAYMENTRESPONSE VARCHAR(MAX),
				PAYMENTGATEWAYID INT,
				ADDITIONALCONFIG VARCHAR(MAX),
				CREATEDDATE DATETIME
			)
	')

END
GO

/****** Object:  StoredProcedure [dbo].[POS_USP_R_BILL]    Script Date: 11-05-2025 18:12:27 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROCEDURE [dbo].[POS_USP_R_BILL]                                 
 @BillID INT,                     
 @DaySequenceID INT                                
AS                                
BEGIN                                
                                
	 DECLARE @LastBillID INT                                
	 SELECT @LastBillID = LASTBILLID FROM POS_DAYSEQUENCE WHERE DAYSEQUENCEID = @DaySequenceID                                
                                
	 DECLARE @LastBilledAmount DECIMAL(11, 2)                                
	 DECLARE @LastBilledQuantity INT                                
                                
	 SELECT @LastBilledAmount = SUM(BD.BILLEDAMOUNT), @LastBilledQuantity = SUM(BD.QUANTITY)           
	 FROM POS_BILLDETAIL BD WHERE BD.BILLID = @LastBillID AND BD.DELETEDDATE IS NULL                                
                                
	 SELECT                                 
		  B.BILLID, B.BILLNUMBER, B.BILLSTATUS,                             
		  @LastBilledAmount AS LASTBILLEDAMOUNT, @LastBilledQuantity AS LASTBILLEDQUANTITY  ,                            
		  @LastBillID AS LASTBILLID, B.CREATEDDATE, B.ROUNDING,B.ISDOORDELIVERY,
		  B.CUSTOMERNAME,B.CUSTOMERNUMBER,B.CUSTOMERGST, B.TENDEREDCASH,B.TENDEREDCHANGE
	  FROM                                 
		  POS_BILL B                                
		  LEFT JOIN POS_BILL LASTBILL ON LASTBILL.BILLID = @LastBillID                                 
	 WHERE B.BILLID = @BillID                                
                                
	 SELECT                                
		  BD.BILLDETAILID, B.BILLID, IP.ITEMPRICEID
		  , CASE WHEN BD.DELETEDDATE IS NULL THEN BD.SNO ELSE NULL END SNO
		  , I.ITEMNAME, IC.ITEMCODE, ISNULL(IC.HSNCODE,'') AS HSNCODE,IP.MRP                              
		  ,IP.SALEPRICE, GST.GSTCODE, BD.QUANTITY,                        
		  BD.WEIGHTINKGS, BD.BILLEDAMOUNT,                        
		  BD.CGST, BD.SGST, BD.IGST, BD.CESS,                        
		  BD.GSTVALUE, BD.GSTID,GST.CGST AS CGSTDESC,                        
		  GST.SGST AS SGSTDESC,GST.CESS AS CESSDESC,                        
		  ISNULL(I.ISOPENITEM,0) AS ISOPENITEM, DISCOUNT, BD.OFFERID, OFRTYP.OFFERTYPECODE     
		  , BD.DELETEDDATE
	 FROM                                
		  POS_BILLDETAIL BD                                
		  INNER JOIN POS_BILL B ON B.BILLID = BD.BILLID                                
		  INNER JOIN POS_ITEMPRICE IP ON IP.ITEMPRICEID = BD.ITEMPRICEID                                
		  INNER JOIN POS_ITEMCODE IC ON IC.ITEMCODEID = IP.ITEMCODEID                                
		  INNER JOIN POS_ITEM I ON I.ITEMID = IC.ITEMID                                
		  INNER JOIN POS_GSTDETAIL GST ON GST.GSTID = IP.GSTID                 
		  LEFT JOIN POS_OFFER OFR ON OFR.OFFERID = BD.OFFERID            
		  LEFT JOIN POS_OFFERTYPE OFRTYP ON OFRTYP.OFFERTYPEID = OFR.OFFERTYPEID            
	 WHERE B.BILLID = @BillID --AND BD.DELETEDDATE IS NULL              
	 ORDER BY BD.SNO          
	 
	 SELECT PGWTD.BILLID, PGWTD.MOPID, PGWTD.AMOUNT, PGWTD.PAYMENTREQUEST, PGWTD.PAYMENTRESPONSE, PGWTD.ADDITIONALCONFIG
		, PGWTD.CREATEDDATE
	 FROM POS_PGW_TRANSACTIONDATA PGWTD
	 WHERE PGWTD.BILLID = @BillID
END
GO

CREATE OR ALTER PROC USP_C_PGW_TRANSACTIONDATA
(
	@BillID INT,
	@MopID INT,
	@Amount DECIMAL(18, 2),
	@PaymentRequest VARCHAR(MAX),
	@PaymentResponse VARCHAR(MAX),
	@PaymentGatewayID INT,
	@AdditionalConfig VARCHAR(MAX)
)
AS
BEGIN

	INSERT INTO POS_PGW_TRANSACTIONDATA(BILLID, MOPID, AMOUNT, PAYMENTREQUEST, PAYMENTRESPONSE, PAYMENTGATEWAYID, ADDITIONALCONFIG, CREATEDDATE)
	SELECT @BillID, @MopID, @Amount, @PaymentRequest, @PaymentResponse, @PaymentGatewayID, @AdditionalConfig, GETDATE()

	SELECT SCOPE_IDENTITY() AS TRANSACTIONDATAID

END
GO

/****** Object:  StoredProcedure [dbo].[POS_USP_FINISH_BILL]    Script Date: 15-06-2025 10:52:05 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER   PROCEDURE [dbo].[POS_USP_FINISH_BILL]      
 @BillID INT      
 , @UserID INT      
 , @DaySequenceID INT      
 , @CustomerNumber VARCHAR(10) = NULL      
 , @CustomerName VARCHAR(100) = NULL      
 , @CustomerGST VARCHAR(100) = NULL      
 , @Rounding DECIMAL(3, 2)
 , @IsDoorDelivery BIT
 , @TenderedCash DECIMAL(11, 2) = 0.00
 , @TenderedChange DECIMAL(11, 2) = 0.00
 , @MopValues  POS_BILLMOPVALUES READONLY      
AS      
BEGIN      
    
	IF NOT EXISTS
	(
		SELECT 1
		FROM @MopValues      
		WHERE MOPVALUE <> 0 
	)
	BEGIN
		SELECT 'Payment details missing, please retry or contact administrator if the problem persists'
		RETURN
	END

	INSERT INTO POS_BILLMOPDETAIL(BILLID, MOPID, MOPVALUE,CREATEDDATE)      
	SELECT @BillID, MOPID, MOPVALUE,GETDATE()  
	FROM @MopValues      
	WHERE MOPVALUE <> 0      
      
	UPDATE POS_BILL      
	SET       
		BILLSTATUS = 1      
		, CUSTOMERNAME = @CustomerName      
		, CUSTOMERNUMBER = @CustomerNumber 
		, CUSTOMERGST = @CustomerGST
		, ISDOORDELIVERY = @IsDoorDelivery
		, TENDEREDCASH = @TenderedCash
		, TENDEREDCHANGE = @TenderedChange
		, UPDATEDBY = @UserID      
		, UPDATEDDATE = GETDATE()
		, BILLCLOSEDBY = @UserID
		, BILLCLOSEDDATE = GETDATE()
		, ROUNDING = @Rounding  
	WHERE BILLID = @BillID       
       
	UPDATE POS_DAYSEQUENCE      
	SET LASTBILLID = @BillID, UPDATEDATE = GETDATE()
	WHERE DAYSEQUENCEID = @DaySequenceID      
      
	EXEC POS_USP_R_GETNEXTBILL @UserID, @DaySequenceID    
 
END
GO

-- script for sync logic, to be uncommented after cloud changes

DROP PROC [dbo].[POS_USP_CU_BRANCHCOUNTER]
GO


DROP TYPE [dbo].[POS_BRANCHCOUNTERTYPE]
GO


CREATE TYPE [dbo].[POS_BRANCHCOUNTERTYPE] AS TABLE(
	[COUNTERID] [int] NOT NULL,
	[COUNTERNAME] [nvarchar](50) NULL,
	[BRANCHID] [int] NULL,
	[CREATEDDATE] [datetime] NULL,
	[UPDATEDDATE] [datetime] NULL,
	[DELETEDDATE] [datetime] NULL
	, [PAYMENTGATEWAYID] INT NULL 
	, [PAYMENTGATEWAYADDITIONALCONFIG] [NVARCHAR](MAX) NULL
)
GO


CREATE PROC [dbo].[POS_USP_CU_BRANCHCOUNTER]
(
	@BranchCounters POS_BRANCHCOUNTERTYPE READONLY
)
AS
BEGIN
	UPDATE BC
	SET
		BC.COUNTERNAME	 = UBC.COUNTERNAME
		, BC.BRANCHID	 = UBC.BRANCHID
		, BC.CREATEDDATE = UBC.CREATEDDATE
		, BC.UPDATEDDATE = UBC.UPDATEDDATE
		, BC.DELETEDDATE = UBC.DELETEDDATE
		, BC.PAYMENTGATEWAYID = UBC.PAYMENTGATEWAYID
		, BC.PAYMENTGATEWAYADDITIONALCONFIG = UBC.PAYMENTGATEWAYADDITIONALCONFIG
	FROM
		POS_BRANCHCOUNTER BC
		INNER JOIN @BranchCounters UBC ON UBC.COUNTERID = BC.COUNTERID

	INSERT INTO POS_BRANCHCOUNTER(COUNTERID, COUNTERNAME, BRANCHID, CREATEDDATE, UPDATEDDATE, DELETEDDATE
		, PAYMENTGATEWAYID, PAYMENTGATEWAYADDITIONALCONFIG)
	SELECT COUNTERID, COUNTERNAME, BRANCHID, CREATEDDATE, UPDATEDDATE, DELETEDDATE
		, PAYMENTGATEWAYID, PAYMENTGATEWAYADDITIONALCONFIG
	FROM @BranchCounters UBC
	WHERE NOT EXISTS ( SELECT 1 FROM POS_BRANCHCOUNTER BCINNER WHERE BCINNER.COUNTERID = UBC.COUNTERID)

END
GO

/****** Object:  StoredProcedure [dbo].[USP_R_GETSYNCDATA]    Script Date: 25-06-2025 08:37:52 ******/
SET ANSI_NULLS ON
GO
SET QUOTED_IDENTIFIER ON
GO
ALTER PROC [dbo].[USP_R_GETSYNCDATA]                      
(                      
 @EntityName VARCHAR(50)                      
 ,  @SyncDate DATETIME                       
)                      
AS                      
BEGIN                      
                       
 IF @EntityName = 'POS_BILL'                      
 BEGIN                      
  SELECT BILLID, BILLNUMBER, CREATEDBY, CREATEDDATE, UPDATEDBY, UPDATEDDATE, DELETEDBY,                   
  DELETEDDATE, BILLSTATUS, CUSTOMERNUMBER, CUSTOMERNAME,DAYCLOSUREID , 
  ROUNDING, B.SPLDISCPER, B.ISDOORDELIVERY, B.TENDEREDCASH, B.TENDEREDCHANGE,B.CUSTOMERGST,
  BILLCLOSEDBY,BILLCLOSEDDATE
  FROM POS_BILL B                      
  WHERE                      
   B.CREATEDDATE > @SyncDate                      
   OR B.UPDATEDDATE > @SyncDate                      
   OR B.DELETEDDATE > @SyncDate                      
  RETURN                      
 END                      
                      
 IF @EntityName = 'POS_BILLDETAIL'                      
 BEGIN                      
  SELECT BILLDETAILID, BILLID, ITEMPRICEID, QUANTITY, WEIGHTINKGS,                   
  BILLEDAMOUNT, CREATEDDATE, UPDATEDDATE, DELETEDDATE, CGST                      
   , SGST, IGST, CESS, GSTVALUE, GSTID, SNO, DISCOUNT,OFFERID,DAYCLOSUREID    
  FROM POS_BILLDETAIL BD                      
  WHERE                      
   BD.CREATEDDATE > @SyncDate                      
   OR BD.UPDATEDDATE > @SyncDate                      
   OR BD.DELETEDDATE > @SyncDate                      
  RETURN                      
 END                     
                     
 IF @EntityName = 'POS_BILLMOPDETAIL'                      
 BEGIN                      
  SELECT BILLMOPDETAILID,BILLID,MOPID,MOPVALUE,CREATEDDATE,UPDATEDDATE,DAYCLOSUREID    
  FROM POS_BILLMOPDETAIL BMOP                    
  WHERE                      
   BMOP.CREATEDDATE > @SyncDate                      
   OR BMOP.UPDATEDDATE > @SyncDate                      
  RETURN                      
 END                     
                       
 IF @EntityName = 'STOCKDISPATCH'                      
 BEGIN                      
  SELECT STOCKDISPATCHID, FROMBRANCHID, TOBRANCHID, STATUS, STATUSAPPROVEDBY, STATUSAPPROVEDDATE,                   
  CREATEDBY, CREATEDDATE, UPDATEDBY, UPDATEDATE, DELETEDBY, DELETEDDATE, DISPATCHNUMBER                      
  FROM POS_STOCKDISPATCH SD                      
  WHERE                      
   SD.CREATEDDATE > @SyncDate                      
   OR SD.UPDATEDATE > @SyncDate                      
   OR SD.DELETEDDATE > @SyncDate                      
  RETURN                      
 END                      
                      
 IF @EntityName = 'STOCKDISPATCHDETAIL'                      
 BEGIN                      
  SELECT STOCKDISPATCHDETAILID, STOCKDISPATCHID, ITEMPRICEID, TRAYNUMBER, DISPATCHQUANTITY,                   
  RECEIVEDQUANTITY, CREATEDDATE, UPDATEDATE, DELETEDDATE, WEIGHTINKGS,ISACCEPTED    
  FROM POS_STOCKDISPATCHDETAIL SDD                      
  WHERE                      
   SDD.CREATEDDATE > @SyncDate                      
   OR SDD.UPDATEDATE > @SyncDate                      
   OR SDD.DELETEDDATE > @SyncDate                      
  RETURN                      
 END                      
                       
 IF @EntityName = 'USER'                      
 BEGIN                      
  SELECT USERID, ROLEID, REPORTINGLEADID, CATEGORYID,                     
  BRANCHID, USERNAME, PASSWORDSTRING, FULLNAME, CNUMBER, EMAIL,                     
  ISOTP, GENDER, DOB, CREATEDDATE, UPDATEDDATE, DELETEDDATE                      
  FROM POS_TBLUSER USR                      
  WHERE                      
   USR.CREATEDDATE > @SyncDate                      
   OR USR.UPDATEDDATE > @SyncDate                      
   OR USR.DELETEDDATE > @SyncDate                      
  RETURN                      
 END          
 
 IF @EntityName = 'POS_DAYCLOSURE'                      
 BEGIN                      
  SELECT DAYCLOSUREID,CLOSUREDATE,BRANCHCOUNTERID,OPENINGBALANCE,      
CLOSINGBALANCE,CLOSINGDIFFERENCE,CLOSEDBY,REFUNDAMOUNT,                    
CREATEDDATE,UPDATEDDATE, BILLCOUNT, BILLDETAILCOUNT, CREFUNDCOUNT                    
  FROM POS_DAYCLOSURE DC                    
  WHERE                      
   DC.CREATEDDATE > @SyncDate                      
   OR DC.UPDATEDDATE > @SyncDate                      
  RETURN                      
 END                     
                    
  IF @EntityName = 'POS_DAYCLOSUREDETAIL'            
 BEGIN                      
  SELECT DAYCLOSUREDETAILID,DAYCLOSUREID,DENOMINATIONID,                    
CLOSUREVALUE,MOPID,CREATEDDATE,UPDATEDDATE                    
  FROM POS_DAYCLOSUREDETAIL DCD                    
  WHERE                      
   DCD.CREATEDDATE > @SyncDate                      
   OR DCD.UPDATEDDATE > @SyncDate                      
  RETURN                      
 END                     
                    
  IF @EntityName = 'POS_CREFUND'                      
 BEGIN                      
  SELECT REFUNDID,BILLDETAILID,REFUNDQUANTITY,REFUNDWEIGHTINKGS,                    
REFUNDAMOUNT,CREATEDBY,CREATEDDATE,UPDATEDBY,UPDATEDDATE,                    
DELETEDBY,DELETEDDATE,DAYCLOSUREID                    
  FROM POS_CREFUND CR                    
  WHERE                      
   CR.CREATEDDATE > @SyncDate                      
   OR CR.UPDATEDDATE > @SyncDate                      
  RETURN                      
 END                     
                    
 IF @EntityName = 'POS_BREFUND'                      
 BEGIN                      
  SELECT BREFUNDID,BRANCHID,CREATEDBY,CREATEDATE,                    
UPDATEDBY,UPDATEDDATE,DELETEDBY,DELETEDDATE,STATUS,BREFUNDNUMBER, CATEGORYID                    
  FROM POS_BREFUND BR                    
  WHERE                      
   BR.CREATEDATE > @SyncDate                      
   OR BR.UPDATEDDATE > @SyncDate                      
  RETURN                      
 END                     
                    
 IF @EntityName = 'POS_BREFUNDDETAIL'                      
 BEGIN                      
  SELECT BREFUNDDETAILID,BREFUNDID,ITEMPRICEID,QUANTITY,                    
WEIGHTINKGS,CREATEDDATE,UPDATEDDATE,SNO,TRAYNUMBER,REASONID,DELETEDDATE, REFUNDDESCRIPTION
  FROM POS_BREFUNDDETAIL BRD                    
  WHERE                      
   BRD.CREATEDDATE > @SyncDate                      
   OR BRD.UPDATEDDATE > @SyncDate                      
   OR BRD.DELETEDDATE > @SyncDate                      
  RETURN                      
 END          
     
 IF @EntityName = 'POS_DAYSEQUENCE'                      
 BEGIN                      
  SELECT OPENDATE, LASTUSEDBILLNUM, ISCLOSED, BRANCHCOUNTERID, LASTBILLID, DAYSEQUENCEID, OPENBILLID    
 , CREATEDATE, UPDATEDATE               
  FROM POS_DAYSEQUENCE DS    
  WHERE                      
   DS.CREATEDATE > @SyncDate                      
   OR DS.UPDATEDATE > @SyncDate                      
  RETURN                      
 END      

  IF @EntityName = 'POS_DAYSEQUENCE'                      
 BEGIN                      
  SELECT OPENDATE, LASTUSEDBILLNUM, ISCLOSED, BRANCHCOUNTERID, LASTBILLID, DAYSEQUENCEID, OPENBILLID    
 , CREATEDATE, UPDATEDATE               
  FROM POS_DAYSEQUENCE DS    
  WHERE                      
   DS.CREATEDATE > @SyncDate                      
   OR DS.UPDATEDATE > @SyncDate                      
  RETURN                      
 END      

 IF @EntityName = 'POS_PGW_TRANSACTIONDATA'          
	BEGIN          
		SELECT 
			TRANSACTIONDATAID
			,BILLID          
			,MOPID           
			,AMOUNT          
			,PAYMENTREQUEST  
			,PAYMENTRESPONSE 
			,PAYMENTGATEWAYID
			,ADDITIONALCONFIG
			,CREATEDDATE     
		FROM POS_PGW_TRANSACTIONDATA TRS 
		WHERE CREATEDDATE > @SyncDate               
	RETURN          
	END     
                       
END
GO

IF EXISTS (SELECT 1 FROM SYS.PROCEDURES WHERE [NAME] = 'POS_USP_CU_PAYMENTGATEWAYINFO')
BEGIN
	EXEC('DROP PROC POS_USP_CU_PAYMENTGATEWAYINFO')
END
GO

IF EXISTS (SELECT 1 FROM SYS.TYPES WHERE [NAME] = 'POS_PAYMENTGATEWAYINFOTYPE')
BEGIN
	EXEC('DROP TYPE POS_PAYMENTGATEWAYINFOTYPE')
END
GO

/****** Object:  UserDefinedTableType [dbo].[POS_PAYMENTGATEWAYINFOTYPE]    Script Date: 25-06-2025 12:14:25 ******/
CREATE TYPE [dbo].[POS_PAYMENTGATEWAYINFOTYPE] AS TABLE(
	[PAYMENTGATEWAYINFOID] [int] NOT NULL,
	[PAYMENTGATEWAYINFONAME] [varchar](100) NOT NULL,
	[PAYMENTGATEWAYCONFIGDATA] [varchar](max) NOT NULL,
	[PAYMENTGATEWAYINFOTYPE] [varchar](100) NOT NULL,
	[CREATEDBY] [int] NULL,
	[CREATEDDATE] [datetime] NULL,
	[UPDATEDBY] [int] NULL,
	[UPDATEDDATE] [datetime] NULL,
	[DELETEDBY] [int] NULL,
	[DELETEDDATE] [datetime] NULL
)
GO


/****** Object:  StoredProcedure [dbo].[POS_USP_CU_PAYMENTGATEWAYINFO]    Script Date: 25-06-2025 08:37:11 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE OR ALTER PROC [dbo].[POS_USP_CU_PAYMENTGATEWAYINFO]
(
	@PAYMENTGATEWAYINFO POS_PAYMENTGATEWAYINFOTYPE READONLY
)
AS
BEGIN
	UPDATE PGW
	SET
		PGW.PAYMENTGATEWAYINFOID = UPGW.PAYMENTGATEWAYINFOID
		,PGW.PAYMENTGATEWAYINFONAME = UPGW.PAYMENTGATEWAYINFONAME
		,PGW.PAYMENTGATEWAYCONFIGDATA = UPGW.PAYMENTGATEWAYCONFIGDATA
		,PGW.PAYMENTGATEWAYINFOTYPE = UPGW.PAYMENTGATEWAYINFOTYPE
		,PGW.CREATEDBY = UPGW.CREATEDBY
		,PGW.CREATEDDATE = UPGW.CREATEDDATE
		,PGW.UPDATEDBY = UPGW.UPDATEDBY
		,PGW.UPDATEDDATE = UPGW.UPDATEDDATE
		,PGW.DELETEDBY = UPGW.DELETEDBY
		,PGW.DELETEDDATE = UPGW.DELETEDDATE
	FROM
		PAYMENTGATEWAYINFO PGW
		INNER JOIN @PAYMENTGATEWAYINFO UPGW ON PGW.PAYMENTGATEWAYINFOID = UPGW.PAYMENTGATEWAYINFOID

	INSERT INTO PAYMENTGATEWAYINFO
	(
		PAYMENTGATEWAYINFOID
		,PAYMENTGATEWAYINFONAME
		,PAYMENTGATEWAYCONFIGDATA
		,PAYMENTGATEWAYINFOTYPE
		,CREATEDBY
		,CREATEDDATE
		,UPDATEDBY
		,UPDATEDDATE
		,DELETEDBY
		,DELETEDDATE
	)
	SELECT 
		PAYMENTGATEWAYINFOID
		,PAYMENTGATEWAYINFONAME
		,PAYMENTGATEWAYCONFIGDATA
		,PAYMENTGATEWAYINFOTYPE
		,CREATEDBY
		,CREATEDDATE
		,UPDATEDBY
		,UPDATEDDATE
		,DELETEDBY
		,DELETEDDATE
	FROM @PAYMENTGATEWAYINFO UB
	WHERE NOT EXISTS ( SELECT 1 FROM PAYMENTGATEWAYINFO BINNER WHERE BINNER.PAYMENTGATEWAYINFOID = UB.PAYMENTGATEWAYINFOID)

END
GO


-- changes for test data

--UPDATE POS_BRANCHCOUNTER SET PAYMENTGATEWAYID = 1, PAYMENTGATEWAYADDITIONALCONFIG = '{ "StoreID" : "10737583", "ClientID" : "1013073" }'
--GO

UPDATE TBLCONFIG SET CONFIGVALUE = '1.7.1' WHERE CONFIGID = 1
GO


