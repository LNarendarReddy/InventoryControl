USE [C:\Windows\NSRetailPOS\NSRETAILPOS.MDF]
GO
:ON ERROR EXIT
GO

IF NOT EXISTS (SELECT 1 FROM SYS.all_columns WHERE [name] = 'QTYORWEIGHTINKGS' AND [object_id] = OBJECT_ID('POS_ITEMPRICE'))
BEGIN
	EXEC ('ALTER TABLE POS_ITEMPRICE ADD QTYORWEIGHTINKGS DECIMAL(18, 2)')
END
GO

IF NOT EXISTS (SELECT 1 FROM SYS.all_columns WHERE [name] = 'FILTERMRPBYSTOCK' AND [object_id] = OBJECT_ID('POS_BRANCH'))
BEGIN
	EXEC ('ALTER TABLE POS_BRANCH ADD FILTERMRPBYSTOCK BIT')
END
GO

IF ISNULL(OBJECT_ID('POS_USP_CU_BRANCH'), 0) <> 0
BEGIN
	EXEC ('DROP PROCEDURE [dbo].[POS_USP_CU_BRANCH]')
END
GO

IF ISNULL(OBJECT_ID('POS_USP_CU_ITEMPRICE'), 0) <> 0
BEGIN
	EXEC ('DROP PROCEDURE [dbo].[POS_USP_CU_ITEMPRICE]')
END
GO


IF ISNULL(TYPE_ID('POS_BRANCHTYPE'), 0) <> 0
BEGIN
	EXEC ('DROP TYPE [dbo].[POS_BRANCHTYPE]')
END
GO

IF ISNULL(TYPE_ID('POS_ITEMPRICETYPE'), 0) <> 0
BEGIN
	EXEC ('DROP TYPE [dbo].[POS_ITEMPRICETYPE]')
END
GO

/****** Object:  UserDefinedTableType [dbo].[POS_BRANCHTYPE]    Script Date: 09-09-2023 17:41:10 ******/
CREATE TYPE [dbo].[POS_BRANCHTYPE] AS TABLE(
	[BRANCHID] [int] NOT NULL,
	[BRANCHNAME] [varchar](100) NOT NULL,
	[BRANCHCODE] [varchar](5) NOT NULL,
	[ADDRESS] [varchar](500) NULL,
	[PHONENO] [varchar](20) NULL,
	[LANDLINE] [varchar](20) NULL,
	[EMAILID] [varchar](50) NULL,
	[SUPERVISORID] [int] NULL,
	[ISWAREHOUSE] [bit] NULL,
	[CREATEDDATE] [datetime] NULL,
	[UPDATEDATE] [datetime] NULL,
	[DELETEDDATE] [datetime] NULL,
	[STATEID] [int] NULL,
	[MULTIEDITTHRESHOLD] [int] NULL,
	FILTERMRPBYSTOCK BIT
)
GO

/****** Object:  UserDefinedTableType [dbo].[POS_ITEMPRICETYPE]    Script Date: 09-09-2023 17:41:24 ******/
CREATE TYPE [dbo].[POS_ITEMPRICETYPE] AS TABLE(
	[ITEMPRICEID] [int] NOT NULL,
	[ITEMCODEID] [int] NOT NULL,
	[SALEPRICE] [decimal](7, 2) NULL,
	[MRP] [decimal](7, 2) NULL,
	[GSTID] [int] NOT NULL,
	[CREATEDDATE] [datetime] NULL,
	[UPDATEDATE] [datetime] NULL,
	[DELETEDDATE] [datetime] NULL,
	[BRANCHID] [int] NULL,
	[PARENTITEMPRICEID] [int] NULL,
	QTYORWEIGHTINKGS DECIMAL(18, 2)
)
GO

/****** Object:  StoredProcedure [dbo].[POS_USP_CU_BRANCH]    Script Date: 09-09-2023 17:41:46 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROC [dbo].[POS_USP_CU_BRANCH]
(
	@Branches POS_BRANCHTYPE READONLY
)
AS
BEGIN
	UPDATE B
	SET
		B.BRANCHNAME		 = UB. BRANCHNAME
		, B.BRANCHCODE		 = UB.BRANCHCODE
		, B.ADDRESS			 = UB.ADDRESS
		, B.PHONENO			 = UB.PHONENO
		, B.LANDLINE		 = UB.LANDLINE
		, B.EMAILID			 = UB.EMAILID
		, B.SUPERVISORID	 = UB.SUPERVISORID
		, B.ISWAREHOUSE		 = UB.ISWAREHOUSE
		, B.CREATEDDATE		 = UB.CREATEDDATE
		, B.UPDATEDATE		 = UB.UPDATEDATE
		, B.DELETEDDATE		 = UB.DELETEDDATE
		, B.STATEID			 = UB.STATEID
		, B.MULTIEDITTHRESHOLD = UB.MULTIEDITTHRESHOLD
		, B.FILTERMRPBYSTOCK = UB.FILTERMRPBYSTOCK
	FROM
		POS_BRANCH B
		INNER JOIN @Branches UB ON UB.BRANCHID = B.BRANCHID

	INSERT INTO POS_BRANCH(BRANCHID, BRANCHNAME, BRANCHCODE, ADDRESS, PHONENO, LANDLINE, EMAILID, 
			SUPERVISORID, ISWAREHOUSE, CREATEDDATE, UPDATEDATE, DELETEDDATE, STATEID, MULTIEDITTHRESHOLD, FILTERMRPBYSTOCK)
	SELECT BRANCHID, BRANCHNAME, BRANCHCODE, ADDRESS, PHONENO, LANDLINE, EMAILID, 
			SUPERVISORID, ISWAREHOUSE, CREATEDDATE, UPDATEDATE, DELETEDDATE, STATEID ,MULTIEDITTHRESHOLD, FILTERMRPBYSTOCK
	FROM @Branches UB
	WHERE NOT EXISTS ( SELECT 1 FROM POS_BRANCH BINNER WHERE BINNER.BRANCHID = UB.BRANCHID)

END
GO

/****** Object:  StoredProcedure [dbo].[POS_USP_CU_ITEMPRICE]    Script Date: 09-09-2023 17:42:24 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE PROC [dbo].[POS_USP_CU_ITEMPRICE]
(
	@ItemPrices POS_ITEMPRICETYPE READONLY
)
AS
BEGIN
	UPDATE IP
	SET
		IP.ITEMCODEID = UIP.ITEMCODEID
		, IP.SALEPRICE = UIP.SALEPRICE
		, IP.MRP = UIP.MRP
		, IP.GSTID = UIP.GSTID
		, IP.CREATEDDATE = UIP.CREATEDDATE
		, IP.UPDATEDATE = UIP.UPDATEDATE
		, IP.DELETEDDATE = UIP.DELETEDDATE
		, IP.BRANCHID = UIP.BRANCHID
		, IP.PARENTITEMPRICEID = UIP.PARENTITEMPRICEID
		, IP.QTYORWEIGHTINKGS = UIP.QTYORWEIGHTINKGS
	FROM
		POS_ITEMPRICE IP
		INNER JOIN @ItemPrices UIP ON UIP.ITEMPRICEID = IP.ITEMPRICEID

	INSERT INTO POS_ITEMPRICE
	(
		ITEMPRICEID
		, ITEMCODEID
		, SALEPRICE
		, MRP
		, GSTID
		, CREATEDDATE
		, UPDATEDATE
		, DELETEDDATE
		, BRANCHID
		, PARENTITEMPRICEID
		, QTYORWEIGHTINKGS
	)
	SELECT 
		ITEMPRICEID
		, ITEMCODEID
		, SALEPRICE
		, MRP
		, GSTID
		, CREATEDDATE
		, UPDATEDATE
		, DELETEDDATE
		, BRANCHID
		, PARENTITEMPRICEID 
		, QTYORWEIGHTINKGS
	FROM @ItemPrices UIP
	WHERE NOT EXISTS
		(
			SELECT 1 FROM POS_ITEMPRICE IPINNER WHERE IPINNER.ITEMPRICEID = UIP.ITEMPRICEID
		)
END
GO


/****** Object:  StoredProcedure [dbo].[POS_USP_R_USERCREDENTIALS]    Script Date: 09-09-2023 18:00:43 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROCEDURE [dbo].[POS_USP_R_USERCREDENTIALS]        
@BRANCHID INT,        
@BRANCHCOUNTERID INT,        
@USERNAME NVARCHAR(100),        
@PASSWORDSTRING NVARCHAR(200),    
@HDDSNO NVARCHAR(50)    
AS        
BEGIN        
    
	IF NOT EXISTS (SELECT 1 FROM POS_BRANCHCOUNTER BC WHERE BC.COUNTERID = @BRANCHCOUNTERID AND BC.HDDSNO = @HDDSNO)    
	BEGIN    
		 SELECT 'Machine identifier mismatch, application will exit'    
		 RETURN    
	END    
        
	DECLARE @USERID INT        
	SELECT @USERID = USERID   
	FROM 
		POS_TBLUSER USR  
		INNER JOIN POS_TBLROLE R ON R.ROLEID = USR.ROLEID  
	WHERE (BRANCHID = @BRANCHID OR R.ROLENAME = 'Store Admin')  
		AND USERNAME = @USERNAME         
		AND PASSWORDSTRING = @PASSWORDSTRING        

	DECLARE @DBVersion NVARCHAR(50)
	SELECT @DBVersion = CONFIGVALUE FROM TBLCONFIG WHERE CONFIGNAME = 'DBVersion'
        
	IF(@USERID  > 0)        
	BEGIN        
        
		SELECT         
			USERID, USERNAME, PASSWORDSTRING, FULLNAME, CNUMBER, EMAIL, ISOTP, GENDER,        
			U.BRANCHID, BRANCHNAME, BRANCHCODE, ADDRESS, PHONENO, LANDLINE,  
			R.ROLENAME, B.MULTIEDITTHRESHOLD, @DBVersion AS DBVersion, B.FILTERMRPBYSTOCK
		FROM 
			POS_TBLUSER U   
			INNER JOIN POS_BRANCH B ON @BRANCHID = B.BRANCHID        
			INNER JOIN POS_TBLROLE R ON U.ROLEID = R.ROLEID  
		WHERE USERID = @USERID        
      
		SELECT COUNTERID,    
		COUNTERNAME FROM POS_BRANCHCOUNTER      
		WHERE COUNTERID = @BRANCHCOUNTERID      
        
	END        
	ELSE        
	BEGIN        
        
		SELECT 'Invalid Username or Password!'        
        
	END        
        
END
GO

/****** Object:  StoredProcedure [dbo].[POS_USP_R_ITEMMRPLIST]    Script Date: 09-09-2023 18:04:38 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

ALTER PROC [dbo].[POS_USP_R_ITEMMRPLIST]  
@ItemCodeID INT 
, @FilterMRPByStock BIT = 0
AS  
BEGIN  

	SELECT  
		IP.ITEMPRICEID, IP.MRP, IP.SALEPRICE, GST.GSTID, GST.GSTCODE, GST.SGST, GST.CGST, GST.IGST, GST.CESS  
	FROM
		(
			SELECT 
				ISNULL(CIP.ITEMPRICEID,PIP.ITEMPRICEID) AS ITEMPRICEID
			FROM POS_ITEMPRICE PIP
				LEFT JOIN POS_ITEMPRICE CIP ON PIP.ITEMPRICEID = CIP.PARENTITEMPRICEID AND CIP.DELETEDDATE IS NULL
			WHERE PIP.ITEMCODEID = @ItemCodeID AND PIP.DELETEDDATE IS NULL 
			AND PIP.BRANCHID IS NULL
		) IPOUTER
		INNER JOIN POS_ITEMPRICE IP ON IPOUTER.ITEMPRICEID = IP.ITEMPRICEID
		INNER JOIN POS_GSTDETAIL GST ON GST.GSTID = IP.GSTID  
	WHERE
		(@FilterMRPByStock = 0 OR IP.QTYORWEIGHTINKGS > 0)
	ORDER BY IP.ITEMPRICEID DESC

END
GO


/****** Object:  StoredProcedure [dbo].[POS_USP_U_BILLDETAIL]    Script Date: 21-09-2023 13:57:01 ******/
SET ANSI_NULLS ON
GO

SET QUOTED_IDENTIFIER ON
GO

CREATE OR ALTER   PROC [dbo].[POS_USP_U_BILLDETAIL]
(
	@BillDetailID INT
	, @BilledAmount DECIMAL(18, 2)
)
AS
BEGIN
	
	UPDATE BD          
	SET BILLEDAMOUNT = @BilledAmount
		, BD.GSTVALUE = ROUND(@BilledAmount - ((@BilledAmount / (100 + GST.CGST + GST.SGST + GST.CESS)) * 100), 2)          
		, BD.DISCOUNT = ((CASE WHEN I.ISOPENITEM = 1 THEN BD.WEIGHTINKGS ELSE BD.QUANTITY END) * IP.MRP) - @BilledAmount
		, UPDATEDDATE = GETDATE()
		, BD.OFFERID = NULL -- Remove offer as we are giving on fly discount
	FROM          
		POS_BILLDETAIL BD          		
		INNER JOIN POS_GSTDETAIL GST ON GST.GSTID = BD.GSTID
		INNER JOIN POS_ITEMPRICE IP ON IP.ITEMPRICEID = BD.ITEMPRICEID
		INNER JOIN POS_ITEMCODE IC ON IC.ITEMCODEID = IP.ITEMCODEID
		INNER JOIN POS_ITEM I ON I.ITEMID = IC.ITEMID
	WHERE BD.BILLDETAILID = @BillDetailID
  
	UPDATE BD          
	SET           
		BD.CGST = ROUND((((BD.BILLEDAMOUNT - BD.GSTVALUE) * GST.CGST) / 100), 2)          
		, BD.SGST = ROUND((((BD.BILLEDAMOUNT - BD.GSTVALUE) * GST.SGST) / 100), 2)          
		--, BD.IGST = ROUND((BD.BILLEDAMOUNT * GST.IGST) / 100, 2)          
		, BD.CESS = ROUND((((BD.BILLEDAMOUNT - BD.GSTVALUE) * GST.CESS) / 100), 2)          
	FROM          
		POS_BILLDETAIL BD          
		INNER JOIN POS_GSTDETAIL GST ON GST.GSTID = BD.GSTID    
	WHERE BD.BILLDETAILID = @BillDetailID
	
	SELECT                              
		BD.BILLDETAILID, B.BILLID, IP.ITEMPRICEID,                      
		BD.SNO, I.ITEMNAME, IC.ITEMCODE, ISNULL(IC.HSNCODE,'') AS HSNCODE,IP.MRP                            
		,IP.SALEPRICE, GST.GSTCODE, BD.QUANTITY,                      
		BD.WEIGHTINKGS, BD.BILLEDAMOUNT,                      
		BD.CGST, BD.SGST, BD.IGST, BD.CESS,                      
		BD.GSTVALUE, BD.GSTID,GST.CGST AS CGSTDESC,                      
		GST.SGST AS SGSTDESC,GST.CESS AS CESSDESC,                      
		ISNULL(I.ISOPENITEM,0) AS ISOPENITEM, DISCOUNT, BD.OFFERID, OFRTYP.OFFERTYPECODE          
	FROM                              
		POS_BILLDETAIL BD               
		INNER JOIN POS_BILL B ON B.BILLID = BD.BILLID                              
		INNER JOIN POS_ITEMPRICE IP ON IP.ITEMPRICEID = BD.ITEMPRICEID                              
		INNER JOIN POS_ITEMCODE IC ON IC.ITEMCODEID = IP.ITEMCODEID                              
		INNER JOIN POS_ITEM I ON I.ITEMID = IC.ITEMID                              
		INNER JOIN POS_GSTDETAIL GST ON GST.GSTID = IP.GSTID               
		LEFT JOIN POS_OFFER OFR ON OFR.OFFERID = BD.OFFERID          
		LEFT JOIN POS_OFFERTYPE OFRTYP ON OFRTYP.OFFERTYPEID = OFR.OFFERTYPEID   
	WHERE BD.BILLDETAILID = @BillDetailID

END
GO


UPDATE TBLCONFIG SET CONFIGVALUE = '1.4.0' WHERE CONFIGNAME = 'DBVersion'
GO